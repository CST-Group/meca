plugins {
    id 'java-library-distribution'
    id 'jacoco'
    id 'maven-publish'
}

group = 'com.github.CST-Group'
description = "The Multipurpose Enhanced Cognitive Architecture (MECA)"

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

version = '0.8.0'

repositories {
    // Local flat directories for custom jars
    flatDir {
        dirs '../cst-desktop/build/libs'
    }

    // Remote repositories
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven { url = "https://github.com/rosjava/rosjava_mvn_repo/raw/master" }
    maven { url = 'https://cst-group.github.io/cst-dependencies/maven-repo/' }
}

configurations {
    extraLibs
}

dependencies {
    implementation files('../cst-bindings/build/libs/cst-bindings-1.1.1.jar') //RESOLVEU -> estava comentadado
    //implementation project(':cst-bindings')
    // CST Desktop API
    api('com.github.CST-Group:cst-desktop:1.1.1')
    //api(':cst-desktop:1.1.5-full')
    //CST Bindings (flat JAR)
    //api name: 'cst-bindings', ext: 'jar'

    // JUnit and testing
    //testImplementation 'junit:junit:4.12'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // ROS 2 Java bindings
    implementation 'io.github.lambdaprime:jros2client:12.0'
    implementation 'io.github.pinorobotics:jros2services:8.0'
    //implementation 'io.github.lambdaprime:jrosmessages-std_msgs:12.0' // keep commented

    // Optional / logging
    implementation 'ch.qos.logback:logback-classic:1.0.11'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'

    api 'org.ros.rosjava_messages:std_msgs:0.5.11'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'org.ros.rosjava_messages:std_msgs:0.5.11'
}

// forces all changing dependencies (i.e. SNAPSHOTs) to always download
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

jar {
    manifest {
        attributes(
            'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' ')
        )
    }
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
    javadoc.options.addStringOption('Xdoclint:none', '-quiet')
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task uberJar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveClassifier = 'full'
    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

artifacts {
    archives javadocJar, sourcesJar, uberJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

uberJar {
    dependsOn jar
}

test {
    useJUnitPlatform()
    maxParallelForks = 1
}
